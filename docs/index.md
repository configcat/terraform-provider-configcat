---
page_title: "ConfigCat Feature Flags Provider"
description: |-
ConfigCat Feature Flags Provider for Terraform allows you to manage ConfigCat resources from a Terraform script. The Provider uses the standard [ConfigCat Public Management API](https://api.configcat.com/).
---

# ConfigCat Feature Flags Provider

ConfigCat Feature Flags Provider for Terraform allows you to manage ConfigCat resources from a Terraform script. The Provider uses the standard [ConfigCat Public Management API](https://api.configcat.com/).

## About ConfigCat

Manage features and change your software configuration using [ConfigCat feature flags](https://configcat.com), without the need to re-deploy code. A 10 minute trainable dashboard allows even non-technical team members to manage application features.
Supports A/B testing, soft launching or targeting a specific group of users first with new ideas. Deploy any time, release when confident. Open-source SDKs enable easy integration with any web, mobile or backend application.

## Authentication

ConfigCat Feature Flags Provider requires authentication. [Create ConfigCat Public API credentials](https://app.configcat.com/my-account/public-api-credentials).

## Provider registration

```terraform
terraform {
  required_providers {
    configcat = {
      source  = "configcat/configcat"
      version = "~> 4.0"
    }
  }
}

variable "configcat_basic_auth_username" {
  type      = string
  sensitive = true
}

variable "configcat_basic_auth_password" {
  type      = string
  sensitive = true
}

provider "configcat" {
  # Get your ConfigCat Public API credentials at https://app.configcat.com/my-account/public-api-credentials
  basic_auth_username = var.configcat_basic_auth_username
  basic_auth_password = var.configcat_basic_auth_password
}
```

## Example usage

```terraform
# Retrieve your Product
data "configcat_products" "my_products" {
  name_filter_regex = "ConfigCat's product"
}

# Retrieve your Config
data "configcat_configs" "my_configs" {
  product_id        = data.configcat_products.my_products.products[0].product_id
  name_filter_regex = "Main Config"
}

# Retrieve your Environment
data "configcat_environments" "my_environments" {
  product_id        = data.configcat_products.my_products.products[0].product_id
  name_filter_regex = "Test"
}

# Create a Feature Flag/Setting
resource "configcat_setting" "setting" {
  config_id    = data.configcat_configs.my_configs.configs[0].config_id
  key          = "isAwesomeFeatureEnabled"
  name         = "My awesome feature flag"
  hint         = "This is the hint for my awesome feature flag"
  setting_type = "boolean"
  order        = 0
}

# Set a value to the Feature Flag/Setting created above
resource "configcat_setting_value" "setting_value" {
  environment_id = data.configcat_environments.my_environments.environments[0].environment_id
  setting_id     = configcat_setting.setting.id
  value          = "false"
}
```

What is a Feature Flag, Setting, Config, Environment or Product?  
[See Main Concepts in ConfigCat Docs](https://configcat.com/docs/main-concepts)

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `base_path` (String) ConfigCat Public Management API's `base_path`. Defaults to [https://api.configcat.com](https://api.configcat.com).  This can also be sourced from the `CONFIGCAT_BASE_PATH` Environment Variable.
- `basic_auth_password` (String, Sensitive) Get your `basic_auth_password` at [ConfigCat Public API credentials](https://app.configcat.com/my-account/public-api-credentials).  This can also be sourced from the `CONFIGCAT_BASIC_AUTH_PASSWORD` Environment Variable.
- `basic_auth_username` (String) Get your `basic_auth_username` at [ConfigCat Public API credentials](https://app.configcat.com/my-account/public-api-credentials).  This can also be sourced from the `CONFIGCAT_BASIC_AUTH_USERNAME` Environment Variable.